buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
		maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'de.undercouch:gradle-download-task:3.4.2'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.1.0'
	id 'se.bjurr.gitchangelog.git-changelog-gradle-plugin' version '1.56'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'de.undercouch.download'

ext.configFile = file "build.txt"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod_version}"
group = "org.zeith.${config.mod_id}"
archivesBaseName = "${config.mod_id_fancy}-${config.minecraft_version}"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "${config.minecraft_version}-${config.forge_version}"
    runDir = "run"
    mappings = "${config.mcp}"
    makeObfSourceJar = false
	replace 'required-after:hammercore', "required-after:hammercore@[${config.hl_version},)"
	replace 'required-after:hammeranims', "required-after:hammeranims@[${config.ha_version},)"
    replace '@VERSION@', "${config.mod_version}"
	replace 'DEV_BUILD', config.trusted_build == "true" ? "TRUSTED_BUILD" : "PUBLIC_BUILD"
}

repositories {
	mavenCentral()
    maven {
        name = "thiakil"
        url = "http://maven.thiakil.com"
    }
	maven {
		name = "HWYLA TEMP"
		url = "https://raw.github.com/TehNut/temporary-maven-thing/master/maven"
	}
    maven {
        name = "DragonForge"
        url = "https://maven.zeith.org"
    }
    maven {
        name = "jei"
        url =  "http://dvs1.progwml6.com/files/maven"
    }
}

dependencies {
	deobfCompile "org.zeith.HammerLib:HammerLib-${config.minecraft_version}:${config.hl_version}"
	deobfCompile "org.zeith.Visuals:Visuals-${config.minecraft_version}:7r"
	deobfCompile "org.zeith.ColoredLux:ColoredLux-${config.minecraft_version}:${config.cl_version}"
	deobfCompile "org.zeith.hammeranims:HammerAnimations-${config.minecraft_version}:${config.ha_version}"
	deobfCompile "com.azanor.baubles:Baubles:1.12-1.5.2"
	
    deobfCompile "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}:api"
    runtime "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}"
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", config.minecraft_version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
		include 'pack.mcmeta'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
		exclude 'pack.mcmeta'
    }
	rename "mod_id_at.cfg", "META-INF/${config.mod_id}_at.cfg"
}

task DownloadThaumcraft(type: Download) {
    src 'https://media.forgecdn.net/files/2629/23/Thaumcraft-1.12.2-6.1.BETA26.jar'
    dest new File("libs/Thaumcraft-1.12.2-6.1.BETA26.jar")
    overwrite false
}

task DownloadThaumicJEI(type: Download) {
    src 'https://media.forgecdn.net/files/2705/304/ThaumicJEI-1.12.2-1.6.0-27.jar'
    dest new File("libs/ThaumicJEI-1.12.2-1.6.0-27.jar")
    overwrite false
}

task DownloadHwyla(type: Download) {
    src 'https://media.forgecdn.net/files/2568/751/Hwyla-1.8.26-B41_1.12.2.jar'
    dest new File("libs/Hwyla-1.8.26-B41_1.12.2.jar")
    overwrite false
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest {
        attributes 'FMLAT': "${config.mod_id}_at.cfg"
        attributes 'FMLCorePlugin': 'org.zeith.thaumicadditions.asm.TARCore'
        attributes 'FMLCorePluginContainsFMLMod': 'false'
    }
}

task srcJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
	manifest {
        attributes 'FMLAT': "${config.mod_id}_at.cfg"
        attributes 'FMLCorePlugin': 'org.zeith.thaumicadditions.asm.TARCore'
        attributes 'FMLCorePluginContainsFMLMod': 'false'
    }
}

jar {
	manifest {
		attributes 'FMLAT': "${config.mod_id}_at.cfg"
        attributes 'FMLCorePlugin': 'org.zeith.thaumicadditions.asm.TARCore'
        attributes 'FMLCorePluginContainsFMLMod': 'false'
	}
}

artifacts {
    archives deobfJar
	archives srcJar
}

curseforge {
	apiKey = System.env.CURSEFORGE_API_KEY != null ? System.env.CURSEFORGE_API_KEY : '0'
	project {
		id = "${config.cf_project}"
		changelogType = 'html'
		changelog = file("changelog.html")
		releaseType = "${config.release_type}"
		addGameVersion "${config.minecraft_version}"
		mainArtifact(jar) {
		  displayName = "[${config.minecraft_version}] ${config.mod_name} - ${config.mod_version}"
		}
		relations {
            requiredLibrary 'Hammer-Lib'
            requiredLibrary 'Thaumcraft'
			optionalLibrary 'Visuals'
            optionalLibrary 'Colored-Lux'
            optionalLibrary 'Thaumic-Wands'
        }
	}
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        System.env.MC_KS_PASS != null
    }
    keyStore = System.env.MC_KS_PATH
    alias = System.env.MC_KS_ALIAS
    storePass = System.env.MC_KS_PASS
    keyPass = System.env.MC_KS_PASS
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

task signDeobfJar(type: SignJar, dependsOn: deobfJar) {
    onlyIf {
        System.env.MC_KS_PASS != null
    }
    keyStore = System.env.MC_KS_PATH
    alias = System.env.MC_KS_ALIAS
    storePass = System.env.MC_KS_PASS
    keyPass = System.env.MC_KS_PASS
    inputFile = deobfJar.archivePath
    outputFile = deobfJar.archivePath
}

task signSrcJar(type: SignJar, dependsOn: srcJar) {
    onlyIf {
        System.env.MC_KS_PASS != null
    }
    keyStore = System.env.MC_KS_PATH
    alias = System.env.MC_KS_ALIAS
    storePass = System.env.MC_KS_PASS
    keyPass = System.env.MC_KS_PASS
    inputFile = srcJar.archivePath
    outputFile = srcJar.archivePath
}

task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    file = new File("changelog.html")
    untaggedName = "Version ${project.version}"
    fromCommit = "${config.start_git_commit}"
    toRef =  "HEAD"
    gitHubIssuePattern = "nonada123";
    templateContent = """
{{#tags}}
<h1>Changes:</h1>
  <ul>
    {{#commits}}
    <li> <a href="${config.git_commit}" target=_blank> {{{message}}}</a>
</li>
    {{/commits}}
  </ul>
{{/tags}}
"""
}

build.dependsOn signJar
build.dependsOn signDeobfJar
build.dependsOn signSrcJar
tasks.build.dependsOn makeChangelog

afterEvaluate {
    tasks["idea"].dependsOn.add(DownloadThaumcraft);
	tasks["idea"].dependsOn.add(DownloadThaumicJEI);
	tasks["idea"].dependsOn.add(DownloadHwyla);
    tasks["build"].dependsOn.add(DownloadThaumcraft);
	tasks["curseforge${config.cf_project}"].dependsOn.add(signJar)
	tasks["curseforge${config.cf_project}"].dependsOn.add(signDeobfJar)
	tasks["curseforge${config.cf_project}"].dependsOn.add(signSrcJar)
	tasks["curseforge${config.cf_project}"].dependsOn.add(makeChangelog)
}